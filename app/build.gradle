apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'androidx.navigation.safeargs.kotlin'

apply plugin: 'com.google.gms.google-services'

apply plugin: 'io.fabric'

android.applicationVariants.all { variant ->
    variant.outputs.all {
        def date = new Date()
        def formattedDate = date.format('dd_MMM_yyyy')
        outputFileName = "movie_db"
        outputFileName += "-" + formattedDate
        switch (variant.buildType.name) {
            case "release":
                outputFileName += "-prod.apk"
                break
            case "debug":
                outputFileName += "-dev.apk"
                break
            default:
                outputFileName += "-" + variant.buildType.name + ".apk"
                break
        }
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "org.themoviedb"
        minSdkVersion 16
        targetSdkVersion 29
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'BASE_URL', '"https://api.themoviedb.org/3/"'
            buildConfigField 'String', 'API_KEY', '"1a126cf2d173cb236c4e2aace5aa86d1"'
//            signingConfig signingConfigs.production
        }
        debug {
            buildConfigField 'String', 'BASE_URL', '"https://api.themoviedb.org/3/"'
            buildConfigField 'String', 'API_KEY', '"1a126cf2d173cb236c4e2aace5aa86d1"'
        }
    }
    androidExtensions {
        experimental = true
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}


ext {
    //layout
    materialVersion = '1.2.0-alpha04'
    constraintLayoutVersion = '2.0.0-beta4'
    shimmerVersion = '0.4.0'
    circleImageVersion = '3.0.1'
    lottieVersion = '3.3.1'

    //ktx
    ktxVersion = '1.2.0'

    //networking
    okHttpVersion = '3.12.0'
    retrofitVersion = '2.7.1'
    //image processing
    picassoVersion = '2.71828'

    //rx
    rxKotlinVersion = '2.3.0'
    rxAndroidVersion = '2.1.0'
    funktionaleVersion = '1.2'

    //aac
    room_version = '2.2.3'
    lifecycle_version = '2.2.0'
    paging_version = "2.1.1"
    work_version = "2.3.1"
    preference_version = "1.1.0"

    //dependency injection
    daggerVersion = '2.26'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'

    //ktx
    implementation "androidx.core:core-ktx:$ktxVersion"
    implementation "androidx.fragment:fragment-ktx:$ktxVersion"
    implementation "androidx.navigation:navigation-runtime-ktx:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.room:room-ktx:$room_version"

    //For layouting purpose
    implementation "com.google.android.material:material:$materialVersion"
    //Shimmer for loading
    implementation "com.facebook.shimmer:shimmer:$shimmerVersion"
    //Circle ImageView
    implementation "de.hdodenhof:circleimageview:$circleImageVersion"
    //Motion Layout
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    //Animation Lottie
    implementation "com.airbnb.android:lottie:$lottieVersion"
    //Preference Screen
    implementation "androidx.preference:preference:$preference_version"

    //Android Architecture Component
    //ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    //Room for Local Storage
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    //Pagination Libs
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    // WorkManager Kotlin
    implementation "androidx.work:work-runtime-ktx:$work_version"
    //  RxJava2 support
    implementation "androidx.work:work-rxjava2:$work_version"


    //Networking Libs
    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    //OkHttp
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    //Image Processing
    implementation "com.squareup.picasso:picasso:$picassoVersion"

    //RxKotlin & RxAndroid
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlinVersion"

    //Dependency Injection
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"


    // Firebase Analytics
    implementation 'com.google.firebase:firebase-analytics:17.2.2'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
